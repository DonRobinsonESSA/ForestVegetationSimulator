      SUBROUTINE ORIN (DEBUG,LKECHO)
      IMPLICIT NONE
C----------
C  **ORIN  ORGANON--DATE OF LAST REVISION:  01/31/2012
C----------
C
C     ORGANON EXTENSION KEYWORD PROCESSOR.
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
      INCLUDE 'CONTRL.F77'
      INCLUDE 'PLOT.F77'
      INCLUDE 'VOLSTD.F77'
      INCLUDE 'ORGANON.F77'
C
COMMONS
C

      INTEGER    KWCNT
      PARAMETER (KWCNT = 3) ! NUMBER OF ORGANON KEYWORDS. 
                            ! This is the length of the TABLE array

      CHARACTER*8  TABLE(KWCNT)
      CHARACTER*8  KEYWRD
      CHARACTER*8  PASKEY
      CHARACTER*10 KARD(7)
      LOGICAL      LNOTBK(7)
      LOGICAL      LOK
      LOGICAL      LKECHO
      LOGICAL      DEBUG
      REAL         ARRAY(7)

      INTEGER      KODE,IPRMPT,NUMBER,I,I1,I2,IYRTMP
     
C     THIS IS THE TABLE OF KEYWORDS FOR THE EXTENSION
      DATA TABLE /
     >    'END',      ! Option 100
     >    'ORGINFO',   ! Option 200
     >    'ORGVOLS' /  ! Option 300

C
C     **********          EXECUTION BEGINS          **********
C

C     IF ORGANON IS ALREADY ACTIVE, JUST JUMP TO PROCESS KEYWORDS.
      IF (LORGANON) GOTO 10  

      LORGANON = .TRUE.
      LORGVOLS = .FALSE.

 10   CONTINUE
C
C
      CALL KEYRDR (IREAD,JOSTND,DEBUG,KEYWRD,LNOTBK,
     >             ARRAY,IRECNT,KODE,KARD,LFLAG,LKECHO)
C
C  RETURN KODES 0=NO ERROR,1=COLUMN 1 BLANK OR ANOTHER ERROR,2=EOF
C               LESS THAN ZERO...USE OF PARMS STATEMENT IS PRESENT.
C

      IF (KODE.LT.0) THEN
         IPRMPT=-KODE
      ELSE
         IPRMPT=0
      ENDIF
      IF (KODE .LE. 0) GO TO 30
      IF (KODE .EQ. 2) CALL ERRGRO(.FALSE.,2)
      CALL ERRGRO (.TRUE.,6)
      GOTO 10

   30 CONTINUE
      CALL FNDKEY (NUMBER,KEYWRD,TABLE,KWCNT,KODE,.FALSE.,JOSTND)
C
C     RETURN KODES 0=NO ERROR,1=KEYWORD NOT FOUND,2=MISSPELLING.
C
      IF (KODE .EQ. 0) GOTO 90
      IF (KODE .EQ. 1) THEN
         CALL ERRGRO (.TRUE.,1)
         GOTO 10
      ENDIF
      GOTO 90
C
C     SPECIAL END-OF-FILE TARGET
C
   80 CONTINUE
      CALL ERRGRO (.FALSE.,2)
   90 CONTINUE
C
C     PROCESS OPTIONS
C
      GO TO( 100, 200, 300 ), NUMBER

  100 CONTINUE
C                        OPTION NUMBER 1 -- END
C     
      IF(LKECHO .AND. LORGANON) then 

C     IF ORGANON IS ACTIVE, THEN TURN OFF SEVERAL FVS OPTIONS.
C     TURN OFF TRIPLING (NOTRIPLE)
      LTRIP = .FALSE.

C     TURN OFF AUTO-ESTABLISHEMENT MODEL (NOAUTOES)
C      LNOAUTOES = .FALSE.

      WRITE(JOSTND,105) KEYWRD
  105 FORMAT (/1X,A8,'   ORGANON EXTENSION ACTIVE, END OF OPTIONS.')
      END IF


      IF(LKECHO .AND. .NOT. LORGANON) WRITE(JOSTND,106) KEYWRD
  106 FORMAT (/1X,A8,'   COULD NOT LOAD ORGANON DLL.')
      RETURN

      
C----------
C                        OPTION NUMBER 2 -- ORGANON ORGINFO
C----------
C     ORGINFO [VERSION=(1,2,3)]   [EVEN=1/UNEVEN-AGED=0]   [MAXSDI_MORTALITY=1/NO MAXSDI_MORTALITY=0] [USE CALIBS]
  200 CONTINUE
C                        OPTION NUMBER 2 -- ORGANON ORGINFO

C     Convert the first argument to an int for the version \in (1,2,3)
      IF (LNOTBK(1)) then 
C        VERSION = IFIX(ARRAY(1))
        IMODTY = IFIX(ARRAY(1))
C       PERFORM A SIMPLE CHECK TO NOT ALLOW RAP VERSION
C        IF( IMODTY < 2 .OR. IMODTY > 3 ) THEN
        IF( IMODTY .NE. 1 ) THEN
      IF(LKECHO ) WRITE(JOSTND,207) KEYWRD
  207 FORMAT (/1X,A8,'   INVALID VERSION. USING VERSION 1.')
           IMODTY = 1
        END IF
      end if

C     ASSIGN THE UNEVEN-AGES/EVEN-AGED FLAG FROM THE SECOND POSITION
C     DEFAULT IS THAT THE STAND IS EVEN AGED.
C     THIS MUST BE MANUALLY SET TO ZERO FOR UN-EVEN AGED STANDS.
C     1=STAND IS EVEN-AGED, 0=UNEVEN-AGED STAND
      INDS(4) = 1
      IF (LNOTBK(2)) then
        INDS(4) = IFIX(ARRAY(2))
      end if

C     IF THE IAGE VARIABLE IS NOT ZERO, THAT ALSO MEANS THAT IT'S 
C     AN EVEN-AGED STAND AND THE VARIABLE CAN THEN ALSO BE SET 
C     TO ONE.
      IF ( IAGE .GT. 0 ) then
        INDS(4) = 1
      end if
      MANAGD = INDS(4)

C     ASSIGN THE MAX SDI MORTALITY SWITCH TO ON
      INDS(9) = 0
      IF (LNOTBK(3)) then
        INDS(9) = IFIX( ARRAY(3) )
      end if

C     IF THE SDIDEF(N) VARIABLE IS NOT ZERO, THAT ALSO MEANS THAT IT'S 
C     MAXIMUM SDI IS SET, FROM THE DATABASE, AND THE MAX SDI FLAG CAN
C     BE SET TO ONE.
      IF ( SDIDEF(1) .GT. 0.0 ) then
        INDS(9) = 1
      end if


C     DON'T AUTOMATICALLY USE THE CALIBRATIONS FROM THE IMPUTATION 
C     SUBROUTINE CALL (PREPARE) UNLESS THE USER EXPLICITLY REQUESTS IT
C     IF THE USER REQUESTS CALIBS, THEN FOR NOW, USE ALL THREE VALUES.
	! FOR NOW, KILL THE CALIBRATIONS.
	! ASSIGN THE DBH, HEIGHT, AND CR(?) CALIBRATIONS TO ONE.
	! TODO: DOES THIS NEED TO BE CONVERTED UPDATED FROM/TO FVS?
      DO I=1,18
         ACALIB(1,I)	= 1.0 
         ACALIB(2,I)	= 1.0
         ACALIB(3,I)	= 1.0
      ENDDO

      INDS(1) = 0
      INDS(2) = 0
      INDS(3) = 0
      IF (LNOTBK(4)) then
        INDS(1) = IFIX( ARRAY(4) )
        INDS(2) = IFIX( ARRAY(4) )
        INDS(3) = IFIX( ARRAY(4) )
      end if

      IF (LKECHO) then 
C WRITE THE DEBUGING OUTPUT TO THE OUTPUT FILE.
C ORGINFO    ORGANON VERSION (1=SWO,2=NWO,3=SMC) = 2; STAND TYPE (0=UNEVEN-AGED,1=EVEN-AGED)= 0;
C        USE MAX-SDI (0=NO,1=YES) = 1; USE CALIBRATION (0=NO,1=YES) = 1
        WRITE(JOSTND,210) KEYWRD,IMODTY,INDS(4)
  210   FORMAT (/1X,A8,'   ORGANON ',
     >       'VARIANT (1=SWO,2=NWO,3=SMC)=',I2,
     >       '; IS EVEN-AGED (0=NO,1=YES)=',I2,';' )

C     IF THE AGE IN THE DATABASE, ISN'T NULL, THEN THIS FLAG SHOULD 
C     MATCH THAT VALUE.

C     THE CALIBS FLAG TRIGGERS ALL THREE CALIBRATIONS, 
C     SO YOU SHOULD ONLY REPORT THE FIRST HEIGHT/DBH CALIB HERE.
        WRITE(JOSTND,211) INDS(9),INDS(1)
  211   FORMAT ('           ',
     >          ' USE MAX-SDI (0=NO,1=YES)=',I2,
     >          '; USE CALIBS (0=NO,1=YES)=',I2 )

      END IF

      GOTO 10
C     END PROCESSING OF  OPTION NUMBER 2 -- ORGANON VERSION

C----------
C                        OPTION NUMBER 3 -- VOLUMES
C----------
C     ORGVOLS [LOG TRIM]   [MIN LOG LENGTH] [TARGET LOG LENGTH] [STUMP HT] [TOP DIB]
C     ORGVOLS 10.0   12.0 32.0 0.5 6
C     TELLS ORGANON LOG BUCKER TO USE:  10 INCHES OF TRIM,
C                                       12 FOOT TARGET LOG LENGTH
C                                       32 FOOT TARGET LOG LENGTH
C                                       0.5 FOOT STUMP HEIGHT
C                                       6.0 INCHES DIAMETER INSIDE BARK MIN LOG DIA    

C      IF(CFTD .LT. 0.0) CFTD=0.0
C      IF(CFSH .LT. 0.0) CFSH=0.0
C      IF(LOGTD .LE. 0.0) LOGTD=6.0
C      IF(LOGSH .LE. 0.0) LOGSH=0.5
C      IF(LOGTA .LE. 0.0) LOGTA=8.0
C      IF(LOGLL .LE. 0.0) LOGLL=32.0
C      IF(LOGML .LE. 0.0) LOGML=8.0

  300 CONTINUE

      LORGVOLS = .TRUE.

C----------
C     THESE ARE COMMON TO BOTH CUBIC FOOT AND BOARD FOOT.
C----------
      IF(LOGTA .LE. 0.0) LOGTA=8.0
      IF (LNOTBK(1)) then 
        LOGTA =   ARRAY(1)     ! LOG TRIM, IN INCHES.
      end if

      IF(LOGML .LE. 0.0) LOGML=8.0
      IF (LNOTBK(2)) then 
        LOGML =   ARRAY(2)     ! MINIMUM LOG LENGTH, IN FEET.
      end if

      IF(LOGLL .LE. 0.0) LOGLL=32.0
      IF (LNOTBK(3)) then 
        LOGLL =  ARRAY(3)     ! TARGET LOG LENGTH, IN FEET.
      end if

C----------
C     ASSIGN THE BOARD FOOT VOLUME STUMP HEIGHT SPEC
C----------
      IF (LNOTBK(4)) then
        DO I = 1, MAXSP
	    BFSTMP( I ) = ARRAY(4) ! STUMP HEIGHT, IN FEET.
	  END DO
      ELSE
        DO I = 1, MAXSP
	    ARRAY(4)    = 0.5           ! IF IT'S NOT HERE, THEN SIMPLY ASSIGN IT.
	    BFSTMP( I ) = ARRAY(4)      ! STUMP HEIGHT, IN FEET.
	  END DO
      end if

C----------
C     ASSIGN THE BOARD FOOT VOLUME TOP DIAMETER SPEC
C----------
      IF (LNOTBK(5)) then 
        DO I = 1, MAXSP
          BFTOPD( I )  = ARRAY(5)   ! BF LOG TOP DIAMETER, IN INCHES.
	  END DO
      ELSE
        DO I = 1, MAXSP
	    ARRAY(5)     = 6.0        ! IF IT'S NOT HERE, THEN SIMPLY ASSIGN IT.
          BFTOPD( I )  = ARRAY(5)   ! BF LOG TOP DIAMETER, IN INCHES.
	  END DO
      end if

C----------
C     ASSIGN THE CUBIC FOOT VOLUME STUMP HEIGHT
C----------
      IF (LNOTBK(6)) then
        DO I = 1, MAXSP
	    STMP( I ) = ARRAY(6)      ! STUMP HEIGHT, IN FEET.
	  END DO
      ELSE
        DO I = 1, MAXSP
	    ARRAY(6)    = 0.0         ! IF IT'S NOT HERE, THEN SIMPLY ASSIGN IT.
	    STMP( I ) = ARRAY(6)      ! STUMP HEIGHT, IN FEET.
	  END DO
      end if

C----------
C     ASSIGN THE BOARD FOOT VOLUME TOP DIAMETER SPEC
C----------
      IF (LNOTBK(7)) then 
        DO I = 1, MAXSP
          TOPD( I )    = ARRAY(7)   ! FT^3 VOL, MIN. TOP DIAMETER, INCHES.
	  END DO
      ELSE
        DO I = 1, MAXSP
	    ARRAY(7)     = 0.0        ! IF IT'S NOT HERE, THEN SIMPLY ASSIGN IT.
          TOPD( I )    = ARRAY(7)   ! FT^3 VOL, MIN. TOP DIAMETER, INCHES.
	  END DO
      end if

      IF (LKECHO .AND. LORGVOLS ) then

        WRITE(JOSTND,311) KEYWRD, LOGTA,LOGML,LOGLL
  311   FORMAT (/1X,A8,'   ORGANON ',
     >  'LOG TRIM IS ', F4.1, ' INCHES', 
     >  '; MINIMUM LOG LENGTH IS ', F4.1, ' FEET', 
     >  '; TARGET LOG LENGTH IS ', F4.1, ' FEET;' )

        WRITE(JOSTND,312) ARRAY(4), ARRAY(5)
  312   FORMAT ('           ',
     >  ' BF STUMP HEIGHT IS ', F4.1, ' FEET;',
     >  ' BF MIN TOP DIB IS ', F4.1, ' INCHES' )

        WRITE(JOSTND,313) ARRAY(6), ARRAY(7)
  313   FORMAT ('           ',
     >  ' CF STUMP HEIGHT IS ', F4.1, ' FEET;',
     >  ' CF MIN TOP DIB IS ', F4.1, ' INCHES' )

      end if

      GOTO 10 ! finished processing ORGVOLS option

      END

